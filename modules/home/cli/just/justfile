_default:
    just --list

[group("nixos")]
[doc("Runs nixos-rebuild switch")]
deploy flake='.#' target='localhost':
    sudo nixos-rebuild switch \
    --flake {{ flake }} \
    {{ if target != 'localhost' { '--target-host {{ target }}' } else { '' } }}

[group("nixos")]
[doc("Runs nixos-rebuild test")]
test flake='.#':
    sudo nixos-rebuild test \
    --flake {{ flake }}

[group("secrets")]
[doc("Updates secret files, run after adding new keys")]
update-secrets:
    #!/usr/bin/env zsh
    sops updatekeys secrets/**/*

[group("secrets")]
[doc("Grabs a host's SSH key and generates the corresponding age key")]
@get-host-key host:
    nix shell nixpkgs#ssh-to-age nixpkgs#openssh \
    --command ssh-keyscan localhost 2>/dev/null \
    | ssh-to-age 2>/dev/null

[group("secrets")]
[doc("Opens a secrets file for editing")]
@edit-secrets file='secrets/default.yaml':
    sops {{ file }}

[group("security")]
[doc("Enroll security key")]
enroll-security-key:
    #!/usr/bin/env bash
    mkdir -p ~/.config/Yubico
    [ -e ~/.config/Yubico/u2f_keys ] \
        && pamu2fcfg --nouser >> ~/.config/Yubico/u2f_keys \
        || pamu2fcfg > ~/.config/Yubico/u2f_keys

[group("security")]
[doc("Clear enrolled security keys, if any")]
clear-security-keys:
    [ -e ~/.config/Yubico/u2f_keys ] \
        && rm -f ~/.config/Yubico/u2f_keys

[group("theming")]
[doc("Opens the current stylix color scheme in a browser")]
@inspect-theme:
    firefox $(readlink -f /etc/stylix/palette.html)
